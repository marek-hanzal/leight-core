buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven'

group "rocks.leight"
version({ ->
    def version = System.getenv("CI_COMMIT_REF_NAME")
    if (version) {
        return version
    }
    def stdout = new ByteArrayOutputStream()
    try {
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Exception ignored) {
        exec {
            commandLine 'git', 'describe', '--contains', '--all', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
}())

jar {
    manifest {
        attributes 'Implementation-Title': 'Leight Core',
                'Implementation-Vendor': 'rocks.leight',
                'Implementation-Version': version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

repositories {
    maven {
        url "https://artifactory.leight.rocks/artifactory/leight/"
        credentials {
            username = "$artifactory_user"
            password = "$artifactory_password"
        }
    }
}

dependencies {
    compile "org.jetbrains.exposed:exposed:$exposed_version"
    compile "ch.qos.logback:logback-classic:$logback_version"
    compile "com.zaxxer:HikariCP:$hikaricp_version"
    compile "io.github.config4k:config4k:$config4k_version"
    compile "org.postgresql:postgresql:$postgresql_version"
    compile "io.github.microutils:kotlin-logging:$kotlin_logging_version"
    compile "com.beust:klaxon:$klaxon_version"
    compile "org.jsoup:jsoup:$jsoup_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-server-core:$ktor_version"
    compile "io.ktor:ktor-gson:$ktor_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    testImplementation "com.h2database:h2:1.4.+"
}
